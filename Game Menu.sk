# Main Menu
# Version 2.0


on load:
    # so this is is where i would begin looking if i was confused with the skript
    # as you can see it says 'resetGameCore()', that means it is calling the function 'resetGameCore()'
    # and if you check out the function resetGameCore() it just resets a list variable and initializes some values in it
    resetGameCore()

    # initGames() is yet another function
    # All it seems to do is fix up a list variable to contain game data and initialize matches within the function
    # through the function initMatches()

    # initGames("gamename", matchcount, minqueue, maxqueue)
    initGames("skywars", 1, 2, 8)
    initGames("murder", 2, 3, 20)
    initGames("prison", 1, 1, 10)
    initGames("skyblock", 1, 1, 10)

    # this just applies the function resetPlayerData() to all players
    # resetPlayerData seems to just setup a list variable per player
    # so we can store data for each player individually
    loop all players:
        resetPlayerData(loop-player)


# as you can see, this basically says "when a player joins, initialize their data so we can start"
# "storing stuff in it"
on join:
    resetPlayerData(player)



# The Cache, where we grab players data from
function resetPlayerData(p: player):
    set {_uuid} to uuid of {_p}
    if {player::%{_p}%} is not set:
        set {player::%{_p}%} to {_uuid}
    if {leplayer::%{_uuid}%} is not set:
        set {leplayer::%{_uuid}%} to {_p}
    if {leplayer::%{_uuid}%::nick} is not set:
        set {leplayer::%{_uuid}%::nick} to false
    set {player::%{_p}%::game} to false
    set {player::%{_p}%::gamedet} to false


# ######
# Util #
# ######

function singleSound(sound: text, p: player, pitch: number, volume: number):
    play sound "%{_sound}%" with volume {_volume} with pitch {_pitch} at location of {_p} for {_p}

function multiSound(sound: text, p: objects, pitch: number, volume: number):
    loop {_p::*}:
        play sound "%{_sound}%" with volume {_volume} with pitch {_pitch} at location of loop-value for loop-value

function generalReadyPlayer(p: player):
	wait 2 ticks
	heal {_p}
	clear the inventory of {_p}
	set {_p}'s gamemode to adventure

function sendTitleToList(title: text, subtitle: text, players: objects):
    replace all "&" in {_title} with "\\u00a7"
    replace all "&" in {_subtitle} with "\\u00a7"
    loop {_players::*}:
        console command "/execute @e[type=armor_stand,name=anchor,c=1] ~ ~ ~ title %loop-value% times 2 40 2"
        console command "/execute @e[type=armor_stand,name=anchor,c=1] ~ ~ ~ title %loop-value% title ""%{_title}%"""
        console command "/execute @e[type=armor_stand,name=anchor,c=1] ~ ~ ~ title %loop-value% subtitle ""%{_subtitle}%"""

function clearTitleToList(players: objects):
    loop {_players::*}:
        console command "/execute @e[type=armor_stand,name=anchor] ~ ~ ~ title %loop-value% clear"


# This is resetGameCore()
function resetGameCore():
    delete {games::*}
    set {games::manager} to true
    set {games::repo} to true
    add "skywars" to {games::repo::*}
    add "murder" to {games::repo::*}
    add "prison" to {games::repo::*}
    add "skyblock" to {games::repo::*}

function initGames(game: text, count: number, minqueue: number, maxqueue: number):
    if {games::repo::*} contains {_game}:
        set {games::manager::%{_game}%} to true
        initMatches({_game}, {_count}, {_minqueue}, {_maxqueue})
        if {_game} is "skywars":
            set {_itemp} to diamond named "&6Skywars!"
            set line 1 of {_itemp}'s lore to "&7Engage in &acompetitive battle!"
            set {games::manager::skywars::item} to {_itemp}
        else if {_game} is "murder":
            set {_itemp} to iron sword named "&6Murder Mystery!"
            set line 1 of {_itemp}'s lore to "&cComing Soon!"
            set {games::manager::murder::item} to {_itemp}
        else if {_game} is "prison":
            set {_itemp} to diamond_ore named "&6Prison"
            set line 1 of {_itemp}'s lore to "&cComing Soon!"
            set {games::manager::prison::item} to {_itemp}
        else if {_game} is "skyblock":
            set {_itemp} to grass_block named "&6Skyblock"
            set line 1 of {_itemp}'s lore to "&cComing Soon"
            set {games::manager::skyblock::item} to {_itemp}
        else if {_game} is "hunger":
            set {_itemp} to diamond_sword

function initMatches(game: text, count: number, minqueue: number, maxqueue: number):
    set {_start} to 1
    while {_start} <= {_count}:
        set {games::manager::%{_game}%::match%{_start}%} to true
        set {games::manager::%{_game}%::match%{_start}%::minqueue} to {_minqueue}
        set {games::manager::%{_game}%::match%{_start}%::maxqueue} to {_maxqueue}
        set {games::manager::%{_game}%::match%{_start}%::label} to {_start}
        set {games::manager::%{_game}%::match%{_start}%::queued} to true
        set {games::manager::%{_game}%::match%{_start}%::state} to 0
        add 1 to {_start}

function translateShortToLong(game: text) :: text:
    if {_game} is "skywars":
        return "Skywars!"
    else if {_game} is "murder":
        return "Murder Mystery!"
    else if {_game} is "prison":
        return "Prison!"
    else if {_game} is "skyblock":
        return "Skyblock!"

# ok so this is an entry point for the code as well
# because people can open up a menu and trigger different parts
# of the skript
# as you can see it calls the function openQueueMenu for the player

command /menu:
    aliases: /queue , /mm , /m , /game
    trigger:
        openQueueMenu(player)

command /leave:
    trigger:
        if {player::%player%::game} is set:
            pingGame({player::%player%::game}, {player::%player%::gamedet})
        removePlayerFromAllQueues(player)
        toSpawn(player)
        send "&7You were &cremoved &7from all queues."
        singleSound("ui.button.click", player, 1, 10)

function toSpawn(p: player):
    set {_epic} to the location (52, 10, 377) in world "world"
    set {_epic}'s yaw to 180
    set {_epic}'s pitch to 0.001
    teleport {_p} to {_epic}

function removePlayerFromAllQueues(p: player):
    loop {games::manager::*}:
        loop {games::manager::%loop-index%::*}:
            if {games::manager::%loop-index-1%::%loop-index-2%::queued::*} contains {_p}:
                remove {_p} from {games::manager::%loop-index-1%::%loop-index-2%::queued::*}


# openQueueMenu just seems to open the gui
# for each item it adds it binds the function openNestedMenu to the item when you click it

function openQueueMenu(p: player):
    open virtual chest inventory with 6 rows named "&c&nGame Selector" for {_p}
    set {_slot} to 10
    loop {games::manager::*}:
        if {games::manager::%loop-index%::item} is set:
            format gui slot {_slot} of {_p} with {games::manager::%loop-index%::item} to close then run function openNestedMenu(loop-index, {_p})
            add 2 to {_slot}


# open nested menu seems to open the skywars menu since that is all we have created for now
# when someone clicks on one of the games it calls the function gameQueue passing in the game name, the match number
# and the player

function openNestedMenu(word: text, p: player):
    if {_word} is "skywars":
        open virtual chest inventory with 4 rows named "&c&nSkywars Games" for {_p}
        set {_slot} to 0
        loop {games::manager::%{_word}%::*}:
            if {games::manager::%{_word}%::%loop-index%::label} is set:
                set {_sw} to nether star named "&6Game %{games::manager::%{_word}%::%loop-index%::label}%"
                set line 1 of {_sw}'s lore to "&eClick to Join"
                format gui slot {_slot} of {_p} with {_sw} to close then run function gameQueue({_word}, loop-index, {_p})
                add 1 to {_slot}

function gameQueue(game: text, word: text, p: player):
    set {_pass} to true
    if size of {games::manager::%{_game}%::%{_word}%::queued::*} >= {games::manager::%{_game}%::%{_word}%::maxqueue}:
        send "&cThe queue for this match was full." to {_p}
        singleSound("entity.villager.no", {_p}, 1, 10)
        set {_pass} to false
    else if {games::manager::%{_game}%::%{_word}%::state} >= 3:
        send "&cThis match has already started." to {_p}
        singleSound("entity.villager.no", {_p}, 1, 10)
        set {_pass} to false
    else if {games::manager::%{_game}%::%{_word}%::queued::*} contains {_p}:
        send "&cYou are already queued for this match!" to {_p}
        singleSound("entity.villager.no", {_p}, 1, 10)
        set {_pass} to false
    if {_pass} is true:
        addToQueue({_p}, {_game}, {_word})
        if {_game} is "skywars":
            generalReadyPlayer({_p})
            set {_matchnum} to {games::manager::%{_game}%::%{_word}%::label}
            set {player::%{_p}%::game} to "skywars"
            set {player::%{_p}%::gamedet} to {_matchnum}
            set {_init} to 500
            set {_by} to 200
            set {_multiplier} to {_matchnum} - 1
            set {_finalz} to {_by} * {_multiplier}
            add {_init} to {_finalz}
            teleport {_p} to the location (500, 201, {_finalz}) in world "world"
            pingQueue("skywars", {_matchnum})

function pingQueue(game: text, match: number):
    if size of {games::manager::%{_game}%::match%{_match}%::queued::*} >= {games::manager::%{_game}%::match%{_match}%::minqueue}:
        if {games::manager::%{_game}%::match%{_match}%::state} < 2:
            startGame({_game}, {_match})


function startGame(game: text, match: number):
    broadcast "&cYou have joined Skywars!"
    set {games::manager::%{_game}%::match%{_match}%::state} to 2
    set {_lex} to 459
    set {_ley} to 0
    set {_lez} to 452
    set {_epicgames} to {_match} - 1
    set {_epicgames} to {_epicgames} * 200
    add {_epicgames} to {_lez}
    wait 3 seconds
    execute console command "/setblock %{_lex}% %{_ley}% %{_lez}% structure_block 0 0 {metadata:"""",mirror:""NONE"",ignoreEntities:0b,powered:0b,seed:0L,author:"""",rotation:""NONE"",posX:0,mode:""LOAD"",posY:0,sizeX:32,posZ:1,integrity:1.0f,showair:0b,x:459,name:""1sw1"",y:0,z:452,id:""minecraft:structure_block"",sizeY:32,sizeZ:32,showboundingbox:1b}"
    wait 2 ticks
    console command "/setblock %{_lex}% %{_ley}% %({_lez} - 1)% redstone_block 0 0"
    wait 10 ticks
    console command "/fill %{_lex}% %{_ley}% %{_lez}% %{_lex}% %{_ley}% %({_lez} - 1)% air"
    set {_sec} to 20
    loop 20 times:
        loop {games::manager::%{_game}%::match%{_match}%::queued::*}:
            set {_yee} to "s"
            if {_sec} = 1:
                set {_yee} to ""
            send "&6%{_sec}% &esecond%{_yee}%" to loop-value-2
        remove 1 from {_sec}
        wait 1 second
    set {games::manager::%{_game}%::match%{_match}%::state} to 3
    set {_iteration} to 0
    loop {games::manager::%{_game}%::match%{_match}%::queued::*}:
        set loop-value's gamemode to survival
        if {_iteration} = 0:
            set {_loc} to the location 548, 27.5, 503 in world "world"
            set {_loc}'s yaw to 89.7
            set {_loc}'s pitch to 5.7
        else if {_iteration} = 1:
            set {_loc} to the location 540, 27.5, 473 in world "world"
            set {_loc}'s yaw to 50
            set {_loc}'s pitch to 7.2
        else if {_iteration} = 2:
            set {_loc} to the location 510, 27.5, 463 in world "world"
            set {_loc}'s yaw to 2.4
            set {_loc}'s pitch to 5.4
        else if {_iteration} = 3:
            set {_loc} to the location 481, 27.5, 473 in world "world"
            set {_loc}'s yaw to -46.5
            set {_loc}'s pitch to 11.3
        else if {_iteration} = 4:
            set {_loc} to the location 571, 27.5, 503 in world "world"
            set {_loc}'s yaw to -91.5
            set {_loc}'s pitch to 3.6
        else if {_iteration} = 5:
            set {_loc} to the location 483, 27.5, 532 in world "world"
            set {_loc}'s yaw to -137.1
            set {_loc}'s pitch to 3.2
        else if {_iteration} = 6:
            set {_loc} to the location 511, 27.5, 542 in world "world"
            set {_loc}'s yaw to 178.7
            set {_loc}'s pitch to 2.6
        else if {_iteration} = 7:
            set {_loc} to the location 539, 27.5, 534 in world "world"
            set {_loc}'s yaw to 137.1
            set {_loc}'s pitch to 13.2
        add 1 to {_iteration}
        teleport loop-value to {_loc}
        if {_iteration} > 7:
            set {_iteration} to 0

on death:
    if {player::%victim%::game} is "skywars":
        set victim's gamemode to adventure
        toSpawn(victim)
        removePlayerFromAllQueues(victim)
        pingGame("skywars", {player::%victim%::gamedet})
        resetPlayerData(victim)

on quit:
    if {player::%player%::game} is "skywars":
        removePlayerFromAllQueues(player)
        pingGame("skywars", {player::%player%::gamedet})
        resetPlayerData(player)

function pingGame(game: text, match: number):
    if {_game} is "skywars":
        if {games::manager::skywars::match%{_match}%::state} >= 3:
            if size of {games::manager::skywars::match%{_match}%::queued::*} <= 1:
                loop {games::manager::skywars::match%{_match}%::queued::*}:
                    set {_winner} to loop-value
                    generalReadyPlayer(loop-value)
                    toSpawn(loop-value)
                    resetPlayerData(loop-value)
                    removePlayerFromAllQueues(loop-value)
                broadcast "&8[&c&lSinister &4&lNetwork&8] &6%{_winner}% &6won SkyWars!"
                clear {games::manager::skywars::match%{_match}%::queued::*}
                set {games::manager::skywars::match%{_match}%::state} to 0

function addToQueue(p: player, game: text, match: text):
    add {_p} to {games::manager::%{_game}%::%{_match}%::queued::*}
    set {_current} to size of {games::manager::%{_game}%::%{_match}%::queued::*}
    set {_max} to {games::manager::%{_game}%::%{_match}%::maxqueue}
    set {player::%{_p}%::game} to {_game}
    set {player::%{_p}%::match} to {_match}
    loop {games::manager::%{_game}%::%{_match}%::queued::*}:
        send "&a%{_p}% &7joined your game. &8(&e%{_current}%/%{_max}%&8)" to loop-value
        singleSound("ui.button.click", loop-value, 1, 10)
    send "&7Use &c/leave &7to leave the game" to {_p}
